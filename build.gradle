
buildscript {
    ext {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'

        kotlinVersion = '1.0.0'
        vertxVersion = '3.2.1'
        log4jVersion = '2.5'
        gradleVersion = '2.5'
        gsonVersion = '2.6.1'
        hikaricpVersion = '2.4.3'
        mariaDbVersion = '1.3.6'
        guavaVersion = '19.0'
//        kotliqueryVersion = '1.0.1'
        depAnalyzeVersion = '1.0.3'
        jetbrainsExposedVersion = '0.3.2'
        bouncyCastleVersion = '1.54'
        spekVersion = '0.1.195'
        apacheLangVersion = '3.4'

        libs = [
                kotlinLang: "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion",
                kotlinPlugin: "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion",
                logging: [
                        "org.apache.logging.log4j:log4j:$log4jVersion",
                        "org.apache.logging.log4j:log4j-slf4j-impl:$log4jVersion",
                        "org.apache.logging.log4j:log4j-core:$log4jVersion"
                ],
                gson:  "com.google.code.gson:gson:$gsonVersion",
                hikaricp: "com.zaxxer:HikariCP:$hikaricpVersion",
                vertxWeb: [
                        "io.vertx:vertx-web:$vertxVersion",
                        "io.vertx:vertx-auth-common:$vertxVersion"
                ],
                vertxCore: "io.vertx:vertx-core:$vertxVersion",
                thymeleaf: "io.vertx:vertx-web-templ-thymeleaf:$vertxVersion",
                dbDriver: "org.mariadb.jdbc:mariadb-java-client:$mariaDbVersion",
                guava: "com.google.guava:guava:$guavaVersion",
                jetbrainsExposed: "org.jetbrains.exposed:exposed:$jetbrainsExposedVersion",
                depAnalyze: "ca.cutterslade.gradle:gradle-dependency-analyze:$depAnalyzeVersion",
                bouncyCastle: "org.bouncycastle:bcprov-jdk15on:$bouncyCastleVersion",
                apacheLang: "org.apache.commons:commons-lang3:$apacheLangVersion"

                //                kotliquery: "com.github.seratch:kotliquery:$kotliqueryVersion",
        ]

        testLibs = [
                spek: "org.jetbrains.spek:spek:$spekVersion"
        ]

        wrapperDistUrl = "https://services.gradle.org/distributions/gradle-${gradleVersion}-all.zip"
    }

    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath libs.kotlinPlugin
        classpath libs.depAnalyze
    }
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'kotlin'

    group 'org.project'
    version '1.0-SNAPSHOT'
}

subprojects {
    repositories {
        mavenCentral()

        maven {
            url 'http://oss.sonatype.org/content/repositories/snapshots'
        }
        maven {
            url 'https://dl.bintray.com/kotlin/exposed'
        }
        maven {
            url "http://repository.jetbrains.com/all"
        }
    }

    dependencies {
        compile libs.kotlinLang,
                libs.logging
        testCompile testLibs.spek
    }
}

configurations.all {
    resolutionStrategy.eachDependency { details ->
        if (details.requested.name == 'log4j') {
            logger.warn("Skipping $requested dependency in favor of primary dependency version")
            details.useTarget "org.apache.logging.log4j:log4j:$log4jVersion"
        }
    }
}

//apply plugin: 'ca.cutterslade.analyze'
//
//analyzeClassesDependencies {
//    justWarn = true
//}